program "Bounce3"

breed Turtle (pos:vec4, d:vec4, color:vec4)

def setColor() {
  this.color = vec4(this.pos:vec4.xyz / 512, 1)
}

def move() {
  var new = this.pos:vec4 + this.d:vec4
  var newX = new.x
  var newY = new.y
  var newZ = new.z

  var d = this.d

  if (newX < 0.0) {
    newX = -newX
    d = vec4(d.x * -0.9, d.yzw)
  }
  if (newX > u_resolution.x) {
    newX = u_resolution.x - (newX - u_resolution.x)
    d = vec4(d.x * -0.9, d.yzw)
  }

  if (newY < 0.0) {
    newY = mod(newY, u_resolution.y)
    d = vec4(d.x, -0.1, d.zw)
  }
  if (newY > u_resolution.y) {
    newY = u_resolution.y - (newY - u_resolution.y)
    d = vec4(d.x, -d.y, d.zw)
  }

  if (newZ < 0.0) {
    newZ = -newZ;
    d = vec4(d.xy, d.z * -0.9, d.w)
  }
  if (newZ > u_resolution.z) {
    newZ = u_resolution.z - (newZ - u_resolution.z)
    d = vec4(d.xy, d.z * -0.9, d.w)
  }

  this.pos = vec4(newX, newY, newZ, 0)
  this.d = d + vec4(0, -0.01, 0, 0)
}

def set() {
  this.pos = vec4(256, 256, 256, 0)
}

static setup() {
  Turtle.setCount(900000);
  Turtle.set();
  Turtle.fillRandomDir3("d");
  Turtle.setColor();
  loop.start()
}

static loop() {
  Turtle.move()
  Turtle.render()
}
